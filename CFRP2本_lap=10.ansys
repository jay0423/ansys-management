! CFRP２本で引張試験
! 各CFRP長さは65mm
! 継手長さが10mm
! 以下警告が未解決

!  *** WARNING ***                         CP =     148.953   TIME= 16:22:11
!  Structural elements without mid nodes usually produce much more         
!  accurate results in quad or brick shape.                                

!  *** WARNING ***                         CP =     149.109   TIME= 16:22:22
!  Volume 1 is being meshed entirely with SOLID185 tetrahedra, which can  be much less accurate than bricks.  Use quadratic (10-node) tetrahedra  
!  if available.                                                           

!  *** WARNING ***                         CP =     149.188   TIME= 16:22:22
!  Volume 4 is being meshed entirely with SOLID185 tetrahedra, which can be much less accurate than bricks.  Use quadratic (10-node) tetrahedra  
!  if available.                                                           

!  *** WARNING ***                         CP =     154.594   TIME= 16:23:09
!  Meshes made up of 10 percent or more of SOLID185 tetrahedra are not recommended.                                                            

!  *** WARNING ***                         CP =     154.594   TIME= 16:23:09
!  Previous testing revealed that 121 of the 5043 selected elements        
!  violate shape warning limits.  To review warning messages, please see the output or error file, or issue the CHECK command.                   



!/input,c:\ansys\test-line,txt
finish
/clear,start
/NOPR

!-タイトル--------------------------------------------------
/TITLE,TEST

!-総合プリプロセッサ----------------------------------------
/PREP7


!-基本定義--------------------------------------------------
ANTYPE,STATIC        ! 静解析
ET,1,SOLID185       ! 試験片ALとCFRPにSOLID185を使用

/NOPR                !構造解析
KEYW,PR_SET,1   
KEYW,PR_STRUC,1 
KEYW,PR_THERM,0 
KEYW,PR_FLUID,0 
KEYW,PR_ELMAG,0 
KEYW,MAGNOD,0   
KEYW,MAGEDG,0   
KEYW,MAGHFE,0   
KEYW,MAGELC,0   
KEYW,PR_MULTI,0 
KEYW,PR_CFD,0   
/GO 

!-パラメータ定義--------------------------------------------
X1 = 120E-3 !試験片長さ
Z1 = 5E-3 !PLA厚さ
Y2 = 8E-3 !エポキシ部の長さ
Y3 = 10E-3 !PLA横の長さ
Z2 = 3E-3 !エポキシ部の厚さ

X2 = 65E-3 !CFRP長さ
Z3 = 2E-3 !CFRPの厚さ
Y4 = 2E-3 !CFRP1の初期位置
Y5 = 6E-3 !CFRP2の初期位置
Y_CFRP = 2E-3 !CFRPの横長さ

DIV1=2E-3  !PLA, epoxy
DIV2=0.5E-3 !分割数2 CFRP
DIV3=0.4E-3
DIV4=0.2E-3
DIV5=0.2E-3

DISTANCE=-2.5E-3 !N?


!-材料物性定義----------------------------------------------
!>>>>>材料1>>>>>>>
! PLA
Aee1 = 2.78E9!全方向弾性率

Anu12 = 0.3 !全方向ポアソン比

Ag12 = 26E9   !全方向せん断弾性率

!MITUDO2= 2790   !質量密度

     
MP,EX,1,Aee1        ! 材料2:AL
MP,EY,1,Aee1  
MP,EZ,1,Aee1  
MP,PRXY,1,Anu12
MP,PRYZ,1,Anu12
MP,PRXZ,1,Anu12
MP,GXY,1,Ag12
MP,GYZ,1,Ag12
MP,GXZ,1,Ag12




!>>>>>材料2>>>>>>>
! エポキシ樹脂
Bee1 = 2E9!全方向弾性率

Bnu12 = 0.39 !全方向ポアソン比

Bg12 = 26E9   !全方向せん断弾性率

!MITUDO2= 2790   !質量密度

     
MP,EX,2,Bee1        ! 材料2:AL
MP,EY,2,Bee1  
MP,EZ,2,Bee1  
MP,PRXY,2,Bnu12
MP,PRYZ,2,Bnu12
MP,PRXZ,2,Bnu12
MP,GXY,2,Bg12
MP,GYZ,2,Bg12
MP,GXZ,2,Bg12

!MP,DENS,2,MITUDO2

!>>>>>材料3>>>>>>>
! CFRP
Cee1 = 54E9!1方向弾性率
Cee2 = 6E9  !2方向弾性率
Cee3 = 6E9  !3方向弾性率

Cnu12 = 0.15 !zx,zy方向ポアソン比
Cnu21 = 0.15 !xz,yz方向ポアソン比
!Cnu21 = 0.16 !xy,yx方向ポアソン比

Cg23 = 9E9 !2-3方向せん断弾性率
Cg12 = 4E9 !2-3方向せん断弾性率

CsuA = -100E9 !引張強度(繊維平行方向)
CsuX = 701E6 !引張強度(繊維平行方向)
CsuZ = 19E6 !引張強度(繊維垂直方向)
CsuY = 19E6 !引張強度(繊維垂直レイアップ方向)
SendanXYZ = 90.4E6 !せん断許容応力

MITUDO1=  1260  !質量密度



MP,EX,3,Cee1        ! 材料1:CFRTP
MP,EY,3,Cee3  
MP,EZ,3,Cee2 
 
!MP,PRXY,3,Cnu21
!MP,PRYZ,3,Cnu21
!MP,PRXZ,3,Cnu21

MP,NUXY,3,Cnu12
MP,NUYZ,3,Cnu21
MP,NUXZ,3,Cnu12


MP,GXY,3,Cg23
MP,GYZ,3,Cg12
MP,GXZ,3,Cg23

!MP,DENS,1,MITUDO1



!-損傷モデル定義------------------------------------------

TB,DMGI,1,,4,1			! 損傷発生の基準則を指定
TBDATA,1,2,2,2,2			! 最大応力則の指定

TB,FCLI,1,1,20,1			! 損傷発生の閾値を指定
TBDATA,1,CsuX,CsuA,CsuY,CsuA,CsuZ,CsuA! 応力指定
TBDATA,7,SendanXYZ,SendanXYZ,SendanXYZ


TB,DMGE,1,1,4,1			! 損傷発生時の剛性減少率を指定
TBDATA,1,1,1,1,1	! 損傷発生時の剛性減少率を指定








!-キーポイント定義------------------------------------------
! PLA
K	,	1	,	0	,	0	,	0
K	,	2	,	0	,	Y3	,	0
K	,	3	,	X1	,	Y3	,	0
K	,	4	,	X1	,	0	,	0
K	,	5	,	0	,	0	,	Z1
K	,	6	,	0	,	Y3	,	Z1
K	,	7	,	X1	,	Y3	,	Z1
K	,	8	,	X1	,	0	,	Z1
! エポキシ樹脂
K       ,       9       ,       0       ,    (Y3-Y2)/2  ,       (Z1-Z2)/2
K       ,      10       ,       0       ,  (Y3-Y2)/2+Y2 ,       (Z1-Z2)/2
K       ,      11       ,       X1      ,  (Y3-Y2)/2+Y2 ,       (Z1-Z2)/2
K       ,      12       ,       X1      ,  (Y3-Y2)/2    ,       (Z1-Z2)/2
K       ,      13       ,       0       ,    (Y3-Y2)/2  ,       (Z1-Z2)/2+Z2
K       ,      14       ,       0       , (Y3-Y2)/2+Y2  ,       (Z1-Z2)/2+Z2
K       ,      15       ,       X1      ,  (Y3-Y2)/2+Y2 ,       (Z1-Z2)/2+Z2
K       ,      16       ,       X1      ,  (Y3-Y2)/2    ,       (Z1-Z2)/2+Z2
! CFRP1本目
K       ,      17       ,       0       ,      Y4       ,       (Z1-Z3)/2
K       ,      18       ,       0       ,   Y4+Y_CFRP   ,       (Z1-Z3)/2
K       ,      19       ,       X2      ,   Y4+Y_CFRP   ,       (Z1-Z3)/2
K       ,      20       ,       X2      ,      Y4       ,       (Z1-Z3)/2
K       ,      21       ,       0       ,      Y4       ,       (Z1-Z3)/2+Z3
K       ,      22       ,       0       ,    Y4+Y_CFRP  ,       (Z1-Z3)/2+Z3
K       ,      23       ,       X2      ,    Y4+Y_CFRP  ,       (Z1-Z3)/2+Z3
K       ,      24       ,       X2      ,      Y4       ,       (Z1-Z3)/2+Z3
! CFRP2本目
K       ,      25       ,     X1-X2     ,      Y5       ,       (Z1-Z3)/2
K       ,      26       ,     X1-X2     ,  Y5+Y_CFRP    ,       (Z1-Z3)/2
K       ,      27       ,       X1      ,  Y5+Y_CFRP    ,       (Z1-Z3)/2
K       ,      28       ,       X1      ,      Y5       ,       (Z1-Z3)/2
K       ,      29       ,     X1-X2     ,      Y5       ,       (Z1-Z3)/2+Z3
K       ,      30       ,     X1-X2     ,   Y5+Y_CFRP   ,       (Z1-Z3)/2+Z3
K       ,      31       ,       X1      ,   Y5+Y_CFRP   ,       (Z1-Z3)/2+Z3
K       ,      32       ,       X1      ,      Y5       ,       (Z1-Z3)/2+Z3

 
!-ヴォリューム構築------------------------------------------------------

!---試験片--------------------------------------------
! PLA部分
V	,	1	,	2	,	3	,	4	,	5	,	6	,	7	,	8
ALLSEL
CM,SOTO,VOLU
! エポキシ樹脂部分
V	,	9	,	10	,	11	,	12	,	13	,	14	,	15	,	16
ALLSEL
CMSEL,U,SOTO
CM,UCHI,VOLU
ALLSEL
VOVLAP,ALL !重なり部分を削除
ALLSEL
CMSEL,U,UCHI
CM,SOTO,VOLU

! CFRP部分
V	,	17	,	18	,	19	,	20	,	21	,	22	,	23	,	24
V	,	25	,	26	,	27	,	28	,	29	,	30	,	31	,	32
CMSEL,U,SOTO
CM,CFRP,VOLU
ALLSEL
VOVLAP,ALL !重なり部分を削除
ALLSEL
CMSEL,U,SOTO
CMSEL,U,CFRP
CM,UCHI,VOLU


! ALLSEL
! CM,SPECIMEN,VOLU
! VOVLAP,SPECIMEN !重なった部分のヴォリューム作成

! CMSEL,U,CFRP
! VSEL,R,LOC,Z,(Z1-Z2)/2,(Z1-Z2)/2+Z2
! VSEL,R,LOC,Y,(Y3-Y2)/2,(Y3-Y2)/2+Y2

! VSEL,S,LOC,X,0,Y4
! VSEL,S,LOC,Y,0,Y3-Y2
! VSEL,S,VOLU,,9,16

! VSEL,S,LOC,X,0,X1

ALLSEL
VGLUE,ALL

! !-CF部のローカル座標定義--------------------------------------------------

! LOCAL,11,0,0,0,0,0,0,90
! CSYS,0

! !-CF部の繊維方向定義--------------------------------------------------
! !>>>>1層目>>>>>>>>>>>>
! CMSEL,S,CFRP
! VATT,3,,1,11

!-メッシュ分割--------------------------------------------------

CMSEL,S,CFRP
AESIZE,ALL,DIV2

ALLSEL

!-試験片CFRPメッシュ--------------------------------------------
CMSEL,S,CFRP
TYPE,1                             !要素1
MAT,3                             !材料2
MSHAPE,0,3D
MSHKEY,1
VMESH,ALL
MOPT,DEFA
! VSWEEP,ALL
! MOPT,DEFA
CM,CFRP,VOLU


!-試験片外側メッシュ--------------------------------------------
ALLSEL
AESIZE,ALL,DIV1

ALLSEL
CMSEL,U,UCHI
CMSEL,U,CFRP
TYPE,1                             !要素1
MAT,1                             !材料1
VSWEEP,ALL
MOPT,DEFA
CM,SOTO,VOLU
ALLSEL


!-試験片内側メッシュ--------------------------------------------

CMSEL,S,UCHI
TYPE,1                             !要素1
MAT,2                             !材料2
MSHAPE,1,3D
MSHKEY,0
VMESH,ALL
! VSWEEP,ALL
MOPT,DEFA
CM,UCHI,VOLU

!-境界条件指定--------------------------------------------------
ALLSEL

ASEL,S,LOC,X,X1 !グリップ固定
NSLA,S,1 
D,ALL,UZ,0
D,ALL,UX,0
D,ALL,UY,0

! ASEL,S,LOC,Y,0 !グリップ固定
! NSLA,S,1 
! D,ALL,UY,0

ASEL,S,LOC,X,0 !荷重負荷面カップリング
NSLA,S,1
CP,NEXT,UX,ALL

*GET,NNUM,NODE,,NUM,MIN !最小の節点選択
D,NNUM,UX,-1E-3 !節点引張距離




ALLSEL !節点全選択

!-解析------------------------------------------------------
FINISH  
/POST1

  
FINISH  
/SOLU



NSUBST,100,1000,100 ! データベースとファイルの書き込み制御の結果アイテム
TIME,1

OUTRES,ERASE

!OUTRES,ALL,ALL  
OUTRES,NSOL,1    !節点の自由度の解
OUTRES,ESOL,1    !次の要素ソリューションアイテムをすべて有効または無効にする
OUTRES,RSOL,1    !節点反応荷重
OUTRES,NLOAD,1    !要素の節点、入力拘束、および力の荷重（POST1コマンドのPRRFOR、NFORCE、FSUMでも使用され、反力の荷重を計算します）。
OUTRES,MISC,1    !エレメントのその他のデータ（ETABLEコマンドのSMISCおよびNMISCアイテム）


SAVE
SOLVE   



FINISH  


/POST26

RFORCE, 2, NNUM, F, X, FX
prvar,2, !変数2をリストに表示






!-XYせん断応力コンター表示-----------------------------------------------------
/POST1  
SET,LAST


WPLANE, 1, 0, 0, Z1/2, 0, Y3, Z1/2, X1, 0, Z1/2
/CPLANE, 1
/TYPE,1,8

